#program base.
#const width=31.
#const height=30.
#const sprungweite = 5.
#const sprungweiteMin = 3.
#const plattformMin = 2. % Breite der Schutzbereiche zwischen den Feuerschluchten
#const plattformMax = 5.

%#const difficulty = random. %(easy; hard)
#const difficulty = random.



w(1..width).
h(1..height).
w2(2..width-1). % erster und letzter Block sollen begehbar sein.
h2(2..height-1).
v(0..3).
start(1).
end(21).
startX(0,5).
goalX(20,5).
width(width).
height(height).

difficulty(difficulty).

% Hoehenunterschiede zwei aufeinanderfolgender Plattformen
diff(X,X+1):- ph(X;(X+1)).
diff(X,X-1) :- ph(X;(X-1)).
diff(X,X+2):- ph(X;(X+2)), not difficulty(easy).
diff(X,X-2) :- ph(X;(X-2)), not difficulty(easy).

% Graebenbreite
spr(A .. B) :- A=sprungweiteMin, B=sprungweite, difficulty(random).
% Plattformbreite
pla(A .. B) :- A=plattformMin, B=plattformMax, difficulty(random).

% easy - breite Plattformen, wenige schmale Graeben
spr(2 .. 3) :- difficulty(easy).
pla(5 .. 7) :- difficulty(easy).

% medium - breite Plattformen, wenige schmale Graeben
spr(4 .. 5) :- difficulty(medium).
pla(3 .. 5) :- difficulty(medium).


% hard - breite Graeben, schmale Plattformen
spr(4 .. 6) :- difficulty(hard).
pla(1 .. 2) :- difficulty(hard).


% Plattformhoehe
ph(3..6).



%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Genierung der Abschnitte aus Feuergraeben (x-Achse) %
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

% Initialplattform
platform(1,3,1).

% Aufbau der nachfolgenden Plattformen
1{ platform(S,Z,N+1) : w(S), w(Z), S<Z, pla(Z-S), spr(S-Z1)}1 :- platform(S1,Z1,N), Z1<W, width(W).

% Hinzufuegen der Plattformhoehe
1{ platform(S,Z,H,N) : ph(H) }1 :- platform(S,Z,N).

% geringer Hoehenunterschied bei aufeinanderfolgenden Plattformen
:- platform(_,_,H,N), platform(_,_,H1,N+1), not diff(H,H1).

%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Uebersetzung in Bloecke %
%%%%%%%%%%%%%%%%%%%%%%%%%%%

onplatform(X,H) :- platform(A,B,H,_),X>=A, X<=B, w(X).
fire(X) :- not onplatform(X,_), w(X).
% Code fuer Blockmaterialien:
% Nichts/Luft = 0
% normaler Block/solid = 1
% Feuer = 9
% Unterste Bloecke (height) bestehen aus Feuer
block(X-1,Y-1,9) :- fire(X), Y=height.
marked(X,Y) :- fire(X), Y=height.
% Sichere Bereiche
block(X-1,Y-1,1) :- onplatform(X,H), h(Y), Y>=height-H.
marked(X,Y) :- onplatform(X,H), h(Y), Y>=height-H.

block(1..width-1,height,0).

#show block/3.
#show width/1.
#show height/1.
#show startX/2.
#show goalX/2.
%#show platform/3.
#show platform/4.
#show onplatform/2.
%#show fire/1.