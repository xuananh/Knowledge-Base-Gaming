% Level mit typischen Jump and Run-Elementen





#const width=50.
#const height=10.
#const jumpheight=2.
#const jumpwidth=2. 


% Schwierigkeitsgrad kann durch Eingabe-Parameter ueberschrieben werden
#const difficulty = hard.
difficulty(difficulty). 


% Voreingestellte Anzahl bestimmter Objekte
#const cannoncount = 2.
#const firecount=4.
#const firepixelcount=9.
#const platform1Count = 5.
#const platform2Count = 3.
#const platformLength = 7.

#const wallcount=3.
#const questionboxcount=7.
#const enemycount=2.


% Groessenangaben

start(1).
end(30).
startX(0,5).
goalX(49,5).

w(0..width).
h(1..height). 

width(width).
height(height).

block(0..width-1, 0..height-1). 



% Verschiedene Schwierigkeitsstufen
% Abhgaengig von Anzahl der Feuerschluchten, Feinde und fallenden Objekten

focount(0) :- difficulty(easy).
enemycount(1) :- difficulty(easy).
firecount(3) :- difficulty(easy).

focount(2) :- difficulty(medium).
enemycount(3) :- difficulty(medium).
firecount(5) :- difficulty(medium).

focount(5) :- difficulty(hard).
enemycount(6) :- difficulty(hard).
firecount(9) :- difficulty(hard).





#const floorheight = 1.
floor(X,floorheight) :- w(X), not firepixel(X,_).

%%%%%%%%%%%%%%%%%
%%% PLATTFORM %%%
%%%%%%%%%%%%%%%%%


% Platformen / platform1 erste Ebene, platform2 eine darueber. 
pflength(2..X) :- X=platformLength.
pf1height(jumpheight+floorheight..jumpheight+floorheight+1).
platform1Count{ platform1(X, Y, Length) : pflength(Length), pf1height(Y), block(X, Y) }platform1Count.
platform1Length(X,X2) :- platform1(X,Y,L), X2 = X+L. 
:-  platform1Length(A,B),  platform1Length(C,D), A<C, C-1<B.
platform1pixel(A,Y) :- platform1(X,Y,L), X<=A, A<=X+L, w(A).

:- platform1pixel(X,Y), firepixel(X+1,Y).
:- platform1pixel(X,Y), firepixel(X-1,Y).
:- questionBox(X,Y1), firepixel(X-1,Y2), firepixel(X+1,Y2).



platformpixel(X,Y) :- platform1pixel(X,Y).
%platformpixel(X,Y) :- platform2pixel(X,Y).

%%%%%%%%%%%%
%%% FIRE %%%
%%%%%%%%%%%%

% (Feuerschluchten)

firelength(1..jumpwidth).
{ fire(X1,X2) : firelength(Length), w(X1), X2=X1+Length, firecount(Firecount)}12.
:- not Firecount{ fire(X1,X2) : firelength(Length), w(X1), X2=X1+Length, firecount(Firecount)}Firecount, firecount(Firecount).
firepixel(X,floorheight) :- fire(X1,X2), X1<=X, X<=X2, w(X).


% Ausschluesse / Wechselbeziehungen zu anderen Elementen
fireground(X,floorheight) :- w(X).
:- firepixel(X,Y), platformpixel(X,Y+1).
:- firepixel(X,Y), coin(X,Y+1).
:- firepixel(X,Y), questionBox(X,Y+1).
:- firepixel(X,Y), not fireground(X,Y).
:- firepixel(X,Y), firepixel(X+1,Y), firepixel(X+2,Y), firepixel(X+3,Y).
:- firepixel(X,Y), firepixel(X+1,Y), firepixel(X+2,Y), firepixel(X+3,Y), firepixel(X+5,Y).
:- firepixel(X,Y), platform1pixel(X,Y1).


:- not firepixelcount{ firepixel(X,Y) }firepixelcount.


:- platformpixel(X,Y), platformpixel(X,Y+1).
%:- platform1pixel(X,Y), firepixel(X,_).

%:- platform1(X), hole(X).



%%%%%%%%%%%%%%
%%% CANNON %%%
%%%%%%%%%%%%%%

% Kanonen: Abstand von mindestens 10 BlÃ¶cken, Keine Mauer direkt davor, keine weitere storenden Objekte in der Naehe. 

{ cannon(X,Y) : floor(X,Y-1), block(X,Y)}cannoncount. 
{ cannon(X,Y) : platform1pixel(X,Y-1), block(X,Y) }cannoncount.
:- not cannoncount{cannon(X,Y)}cannoncount. 
:- cannon(X,Y), cannon(X1,Y1), X<X1, X+10>X1.
:- cannon(X,Y), wallpixel(XW,Y), XW<X, XW+10>X.
:- cannon(X,Y), cannon(X,Y+1). 
:- cannon(X,Y), not floor(X,Y-1).
:- cannon(X,Y), wallpixel(X,Y).
:- cannon(X,Y), platformpixel(X,Y+2).
:- cannon(X,Y), questionBox(X,Y+2).
:- cannon(X,Y), questionBox(X,Y+1).


%%%%%%%%%%%%
%%% WALL %%%
%%%%%%%%%%%%

% Mauern

wallheight(1+floorheight..jumpheight+floorheight).
wallid(1..wallcount).
wallheight(wallheightMin .. wallheightMax) :- h(wallheightMin; wallheightMax). 
1{ wall(X,Height,ID) : wallheight(Height), w(X) }1 :- wallid(ID).
wall(X) :- wall(X,Height,ID).
wallpixel(X,Y) :- wall(X,H,_), block(X,Y), floorheight<=Y, Y<=floorheight+H.
:- wall(X), platform1pixel(X,_).
:- wall(X1), wall(X2), X1<X2, X2+10<X1. % Mindestabstand
:- wall(X1), wall(X2), X1>X2, X2+10>X1.
:- wall(X,Y,ID1), wall(X1,Y1,ID2),ID1!=ID2, X=X1.



%%%%%%%%%%%%%%%%%%%
%%% QUESTIONBOX %%%
%%%%%%%%%%%%%%%%%%%

% reachableitem(X,Y) :- platformpixel(X1,Y1), Y1-jumpwidth<=Y, Y<=Y1+jumpwidth,X<=Y+jumpheight, block(X,Y).
reachableitem(X,Y) :- Y=jumpheight+floorheight+1, block(X,Y).
reachableitem(X,Y) :- Y=jumpheight+floorheight, block(X,Y).

questionboxcount{ questionBox(X,Y) : reachableitem(X,Y)}questionboxcount.
:- questionBox(X1,Y1), questionBox(X2,Y2), Y1<Y2, X1=X2.
:- questionBox(X,Y1), firepixel(X,Y), Y1=Y+1.
:- questionBox(X,Y1), firepixel(X,Y), Y1=Y+2.
:- questionBox(X,Y1), firepixel(X,Y), Y1=Y+3.
:- questionBox(X,Y), platformpixel(X,Y+1).
:- questionBox(X,Y), platformpixel(X,Y-1).
:- questionBox(X,Y), wallpixel(X,Y-1).
:- questionBox(X,Y), fire(X+1,Y-1), fire(X-1,Y-1).



%%%%%%%%%%%%%
%%% COINS %%%
%%%%%%%%%%%%%

#const coinscount=50.
{ coin(X,Y) : reachableitem(X,Y)}coinscount.
:- questionBox(X,Y), coin(X,Y).
:- coin(X,Y), platformpixel(X,Y).
:- coin(X,Y), cannon(X,Y).
:- coin(X,Y), floor(X,Y).
:- coin(X,Y), enemy(X,Y).
:- coin(X,Y), wallpixel(X,Y).

:- not coinscount{ coin(X,Y) }coinscount.

% Groessere Flaechen von Muenzen
#const coinsareacount=3.
caa(1..coinsareacount).
3{ coinsarea(X,Y,ID) : block(X,Y), not floor(X,Y), not coin(X,Y,ID1), ID1!=ID, caa(ID1) } :- caa(ID).
coin(X1,Y1,ID) 	:- coinsarea(X,Y,ID), X1<=X, X<=X1+3, Y1<=Y, Y<=Y1+3, block(X1,Y1).
coin(X1,Y1) :- coin(X1,Y1,ID).

% Verhindern, dass die Muenzen zu weit oben platziert werden
:- coin(X,Y), Y<height-9.




%%%%%%%%%%%%%
%%% ENEMY %%%
%%%%%%%%%%%%%


{ enemy(X,Y) : floor(X,Y-1)}15. 

:- enemy(X,Y), wallpixel(X,Y).
:- enemy(X,Y), coin(X,Y).
enemy(X) :- enemy(X,_).
:- not Enemycount{ enemy(X,Y) }Enemycount, enemycount(Enemycount).
:- enemy(X), enemy(Z), X<Z, X+7>Z.




%%%%%%%%%%%%%%%%%%%%%%%
%%% FALLING OBJECTS %%%
%%%%%%%%%%%%%%%%%%%%%%%

% Fallende verletzende Objekte
% Muessen mindestens 5 Blocks voneinander entfernt sein. 

{ fallingObject(X,Y) : w(X), Y=1}.
:- not Focount{fallingObject(X,Y)}Focount, focount(Focount).
fallingObject(X) :- fallingObject(X,Y).
:- fallingObject(X), fallingObject(Z), X<Z, X+5>Z.



%%%%%%%%%%%%%%%%%%%%%%
%%% FREIER BEREICH %%%
%%%%%%%%%%%%%%%%%%%%%%

% Avatar soll nicht gleich zu Spielebeginn getoetet werden (Feuerschlucht)
% Ausserdem soll ein guter Levelsegmentuebergang ermoeglicht werden

frei(0..5; width-5..width).
item(X) :- firepixel(X,_).
item(X) :- platformpixel(X,_).
item(X) :- questionBox(X,_).
item(X) :- wallpixel(X,_).
item(X) :- coin(X,_).
item(X) :- cannon(X,_).
item(X) :- enemy(X,_).
item(X) :- fallingObject(X,_).



:- frei(X), item(X).

% UMRECHNUNG IN BLOECKE

block(X,Y,9) :- firepixel(X,height-Y), block(X,Y). 
block(X,Y,0) :- platformpixel(X,height-Y), block(X,Y).
block(X,Y,1) :- floor(X,height-Y), block(X,Y). 
block(X,Y,1) :- wallpixel(X,height-Y), block(X,Y).
block(X,Y,14) :- coin(X,height-Y), block(X,Y). 
block(X,Y,4) :- questionBox(X,height-Y), block(X,Y).
block(X,Y,13) :- cannon(X,height-Y).
block(X,Y,15) :- fallingObject(X,Y).
block(X,Y,5) :- enemy(X,height-Y).
%block(1..width-1,height,0).
%block(1..width,height,0).
block(1..width-1,height,0).

blockid(0..30).
%:- not 0{ block(X,Y,ID)}1, blockid(ID).




#show platform1/3.
#show fire/2.
#show cannon/2.
#show questionBox/2. 
#show coin/2.
#show goalX/2.
#show startX/2.
#show height/1.
#show width/1.
#show block/3.

#show start/1.
#show end/1.